#include sr\utils\_common;

weapons(list)
{
	level.defragStartWeapons = strTok(list, ";");
}

perks(list)
{
	level.mapPerks = strTok(list, ";");
}

triggerSection(id, origin, width, height, callback)
{
	trigger = spawn("trigger_radius", origin, 0, width, height);
	trigger.targetname = "defrag_section";
	trigger.radius = width;
	trigger.callback = callback;
	trigger.id = id;

	return trigger;
}

triggerWeapon(id, origin, width, height, weapon, ammo)
{
	trigger = spawn("trigger_radius", origin, 0, width, height);
	trigger.targetname = "defrag_weapon";
	trigger.radius = width;
	trigger.weapon = weapon;
	trigger.ammo = ammo;
	trigger.id = id;

	return trigger;
}

triggerPerk(id, origin, width, height, perk, time)
{
	trigger = spawn("trigger_radius", origin, 0, width, height);
	trigger.targetname = "defrag_perk";
	trigger.radius = width;
	trigger.perk = perk;
	trigger.time = time;
	trigger.id = id;

	return trigger;
}

switchToDefragWeapon(name)
{
	self switchToWeapon(level.defragWeapons[name]);
}

takeDefragWeapon(name)
{
	self takeWeapon(level.defragWeapons[name]);
}

takeAllPerks()
{
	self.perks = [];
}

takeDefragPerk(id)
{
	self sr\core\_perks::playerRemovePerk(id);
}

giveDefragWeapon(name, ammo)
{
	weapon = level.defragWeapons[name];

	self giveWeapon(weapon);
	self switchToWeapon(weapon);

	if (isDefined(ammo))
		self setWeaponAmmoClip(weapon, ammo);
}

giveDefragPerk(id, time)
{
	self sr\core\_perks::playerSetPerk(id);

	if (isDefined(time))
	{
		wait time;
		self takeDefragPerk(id);
	}
}
